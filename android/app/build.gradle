apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"

// Map Gradle variants to .env files
project.ext.envConfigFiles = [
    devDebug:    ".env.development",
    devRelease:  ".env.development",
    uatDebug:    ".env.uat",
    uatRelease:  ".env.uat",
    prodDebug:   ".env.production",
    prodRelease: ".env.production"
]

// Apply react-native-config dotenv parser AFTER defining envConfigFiles
apply from: project(':react-native-config').projectDir.getPath() + "/dotenv.gradle"

// React Native Plugin Configuration
react {
    autolinkLibrariesWithApp()
}

def enableProguardInReleaseBuilds = false
def jscFlavor = 'io.github.react-native-community:jsc-android:2026004.+'

android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    namespace "com.starterkit"

    defaultConfig {
        applicationId "com.starterkit"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        resValue "string", "build_config_package", "com.starterkit"
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            matchingFallbacks = ['debug', 'release']
        }
        release {
            signingConfig signingConfigs.debug
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    flavorDimensions "default"
    productFlavors {
        dev {
            dimension "default"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            resValue "string", "app_name", "StarterKit DEV"
            buildConfigField "String", "ENV", "\"development\""
        }
        uat {
            dimension "default"
            applicationIdSuffix ".uat"
            versionNameSuffix "-uat"
            resValue "string", "app_name", "StarterKit UAT"
            buildConfigField "String", "ENV", "\"uat\""
        }
        prod {
            dimension "default"
            resValue "string", "app_name", "StarterKit"
            buildConfigField "String", "ENV", "\"production\""
        }
    }
}

dependencies {
    implementation("com.facebook.react:react-android")
    implementation project(':react-native-config')

    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }
}
